package "nova"

import "nova/datastruct/Comparable"
import "nova/datastruct/HashSet"
import "nova/datastruct/list/StringCharArray"

public class String implements Comparable<String>
{
	visible Int    count

	visible StringCharArray chars

	static Char[]  whitespace = [ ' ', '\t', '\n', '\r' ]
	
	external void strcpy(char dst[], char src[])
	external void strcat(char dst[], char src[])
	external int strlen(char chars[])
	external void memcpy(char dst[], char* src[], int count)

	private static HashSet<String> pool = new HashSet(121, 1)

	public Long hashCodeLong
	{
	    get
	    {
            return chars.hashCodeLong
	    }

	    no set
	}
	
	[Target c]
	public construct(Char c)
	{
	    [PrimitiveArray]
		Char[] chars = new Char[2]

		chars[0] = c
		chars[1] = '\0'

		this(chars)
	}
	
	[Target js]
	public construct(Char c)
	{
	    [PrimitiveArray]
		Char[] chars
		
		external
		{
			chars = [#{c}];
		}

		this(chars)
	}
	
	[Target c]
	public construct([PrimitiveArray] Char[] chars)
	{
		count = calculateSize(chars)

		this.chars = new StringCharArray(chars, count)
		
		if (chars[count] != '\0')
		{
			chars[count] = '\0'
		}

		/*if (pool.contains(this))
		{
		    Console.writeLine("Duplicate string #this created")
		}
		else
		{
		    //pool.add(this)
		}*/
	}
	
	[Target js]
	public construct([PrimitiveArray] Char[] chars)
	{
		count = calculateSize(chars)

		this.chars = new StringCharArray(chars, count)
	}

	public construct(Char[] chars)
	{
		count = chars.count

		this.chars = new StringCharArray(chars)
	}

	calculateSize([PrimitiveArray] Char[] chars) -> Int
	{
		[Target c] return strlen(chars)
		
		external js
		{
			return #{chars}.length;
		}
	}

	[Target c]
	public concat(String str) -> String
	{
		Int sz = str.count + count + 1

		[PrimitiveArray]
		Char[] newData = new Char[sz]

		strcpy(newData, chars.data)
		strcat(newData, str.chars.data)

		newData[sz - 1] = '\0'

		return new String(newData)
	}
	
	[Target js]
	public concat(String str) -> String
	{
		external
		{
			var newData = #{chars.data}.slice(0, #{count}).concat(#{str.chars.data});
			
			return new novaConstructors.newString1(newData);
		}
	}
	
	public equals(String other) -> Bool
	{
		return compareTo(other) == 0
	}

	public replace(String search, String replace) -> String
	{
	    String output = this

	    Int index = 0

	    while ((index = output.indexOf(search)) >= 0)
	    {
	        output = output.substring(0, index) + replace + output.substring(index + search.count)
	    }

	    return output
	}

	public startsWith(String search) -> Bool
	{
	    return indexOf(search) == 0
	}

	public endsWith(String search) -> Bool
	{
	    return count > 0 && indexOf(search) == count - search.count - 1
	}

	public contains(String search) -> Bool
	{
	    return indexOf(search) >= 0
	}

	public indexOf(String search, Int start = 0, Int direction = 1, Int defaultReturnValue = -1) -> Int
	{
	    Int i = start

		while (i < count && i >= 0)
		{
			Bool found = true

			Int j = 0

			while (j < search.count && i + j < count && found)
			{
				if (search.chars.get(j) != chars.get(i + j))
				{
					found = false
				}

				j++
			}

			if (found)
			{
//				if (direction > 0)
					return i
//				else
//					return i - search.count + 1
			}

			i += direction
		}

		return defaultReturnValue
	}

	public lastIndexOf(String search) -> Int
	{
		return indexOf(search, count - 1, -1)
	}

	public substring(Int start, Int end = count) -> String
	{
		if (end - start <= 0)
		{
			if (end - start < 0)
            {
                throw new Exception("Substring bounds of [" + start + ", " + end + "] are invalid")
            }
			
            return ""
        }
		
		[PrimitiveArray]
		Char buf[] = new Char[end - start + 1]
		
		[PrimitiveArray]
		Char[] arr = chars.data
		
		external c
		{
			memcpy(#{buf}, &#{arr[start]}, #{end - start});
			
			#{buf[end - start] = '\0'};
		}
		
		external js
		{
			#{buf} = #{arr}.slice(#{start}, #{end});
		}
		
		return new String(buf)
	}

	public lastChar() -> Char
	{
		return charAt(count - 1)
	}

	public charAt(Int index) -> Char
	{
		return chars.get(index)
	}

	public trim() -> String
	{
		Int start = 0
		Int end   = count - 1

		while (start < count && whitespace.contains(chars.get(start)))
		{
			start++
		}

		while (end >= 0 && whitespace.contains(chars.get(end)))
		{
			end--
		}

		if (end == 0)
		{
			return ""
		}

		if (start == 0 && end == count - 1)
		{
			return this
		}

		return substring(start, end + 1)
	}

	public toLowerCase() -> String
	{
		return transform(Char.toLowerCase)
	}

	public toUpperCase() -> String
	{
		return transform(Char.toUpperCase)
	}

	public capitalize() -> String
	{
	    if (count == 0) return this

	    return chars.get(0).toUpperCase() + substring(1)
	}

	public transform(transform(Char, Int) -> Char) -> String
	{
	    [PrimitiveArray]
		Char[] newData = new Char[count]

		for (i in 0..count)
		{
			newData[i] = transform(chars.get(i), i)
		}

		return new String(newData)
	}

	public getStringBetween(String before, String after, Int start = 0) -> String
	{
		Int s = indexOf(before, start)
		Int e = indexOf(after, s + 1)

		if (s >= 0 && e > 0)
		{
			return substring(s + before.count, e)
		}

		return ""
	}

	public surroundWith(String str, Bool symmetrical = false) -> String
	{
		if (symmetrical)
		{
			return str + this + new String(str.chars.reverse())
		}
		else
		{
			return str + this + str
		}
	}

	public compareTo(String other) -> Int
	{
	    var min = Math.min(count, other.count)

		for (i in 0..min)
		{
			if (chars.get(i) - other.chars.get(i) != 0)
			{
				return chars.get(i) - other.chars.get(i)
			}
		}

		return count - other.count
	}

	public toString() -> String
	{
		return this
	}
}
