package "nova/star"

import "nova/star/NativeWindow.h"

class Window {
    public external_name Int x { set { x = value; refreshPosition() } }
    public external_name Int y { set { y = value; refreshPosition() } }
    public external_name Int width { set { width = value; refreshPosition() } }
    public external_name Int height { set { height = value; refreshPosition() } }
    
    public external_name Frame frame
    
    public external_name String title
    
    external type HWND
    external type HDC
    external type PAINTSTRUCT
    
    external Int NULL
    
    [Target c] public external_name HWND hwnd
    [Target c] public external_name HDC hdc
    [Target c] public external_name PAINTSTRUCT ps
    
    [Target c] public external_name WindowThread thread
    
    public construct(public String title = null, public Int x = 0, public Int y = 0, public Int width = 0, public Int height = 0) {
        frame = new Frame(this)
        
        thread = new WindowThread(this)
        thread.start()
    }
    
    public show() => this {
        thread.show()
    }
    
    public refreshPosition() {
        external c {
            SetWindowPos(#{hwnd}, NULL, #{x}, #{y}, #{width}, #{height}, SWP_NOACTIVATE);
        }
    }
    
    public center() => this {
        x = Screen.width / 2 - width / 2
        y = Screen.height / 2 - height / 2
    }
    
    public repaint() {
        if (hwnd != NULL) { // If the window has been loaded
            external c {
                SendMessage(#{hwnd}, WM_USER_INVALRECT, 0, 0);
            }
        }
    }
}