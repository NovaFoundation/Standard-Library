package "nova/datastruct"

import "nova/datastruct/list/List"

[AutoFinal]
[Immutable "ImmutableHashMap"]
class HashMap<K, V> implements List<Pair<K, V>> {
    Pair<K, V>[][] buckets
    
    Int bucketSize
    
    visible var Int count
    
    this[K key] -> V {
        get {
            return getPair(key)?.value
        }
        
        set {
            remove(key)
            
            getBucket(key).add(new Pair<K, V>(key, value))
            
            count++
            
            return this
        }
    }
    
    public construct(Int bucketCount = 5, Int bucketSize = 5) {
        buckets = new Pair<K, V>[bucketCount][bucketSize]
        
        this.bucketSize = bucketSize
    }
    
    public construct(HashMap<K, V> source, Int bucketCount = 5, Int bucketSize = 5) {
        this(bucketCount, bucketSize)
        
        source.forEach(pair -> {
            put(pair.key, pair.value)
        })
    }
    
    public put(K key, V value) => this[key] = value
    
    [Override]
    public toArray() => map({ _ })
    
    [Override]
    public contains(Pair<K, V> value) -> Bool {
        let pair = getPair(value.key)
        
        if (pair != null) {
            return pair.value == value.value// || (pair.value != null && pair.value.equals(value.value))
        }
        
        return false
    }
    
    [Override]
    public any(func(Pair<K, V>) -> Bool) -> Bool {
        for (bucket in buckets) {
            for (pair in bucket) {
                if (pair != null && pair != 0 && func(pair)) {
                    return true
                }
            }
        }
        
        return false
    }
    
    [Override]
    public all(func(Pair<K, V>) -> Bool) -> Bool {
        for (bucket in buckets) {
            for (pair in bucket) {
                if (pair != null && pair != 0 && !func(pair)) {
                    return false
                }
            }
        }
        
        return true
    }
    
    [Override]
    public map<Out>(func(Pair<K, V>, Int, HashMap<K, V>) -> Out) -> Out[] {
        let array = new Out[]
        
        var Int i = 0
        
        for (bucket in buckets) {
            for (pair in bucket) {
                if (pair != null && pair != 0) {
                    array.add(func(pair, i++, this))
                }
            }
        }

        return array
    }
    
    [Override]
    public filter(func(Pair<K, V>, Int, HashMap<K, V>) -> Bool) -> Pair<K, V>[] {
        let array = new Pair<K, V>[]
        
        var Int i = 0
        
        for (bucket in buckets) {
            for (pair in bucket) {
                if (pair != null && pair != 0 && func(pair, i++, this)) {
                    array.add(pair)
                }
            }
        }

        return array
    }
    
    [Override]
    public join(String delimiter) -> String {
        var Int i = 0
        
        var output = ""
        
        for (bucket in buckets) {
            for (pair in bucket) {
                if (pair != null && pair != 0) {
                    if (i > 0) {
                        output = output + delimiter
                    }
                    
                    output = output + pair
                }
            }
        }

        return output
    }
    
    [Override] public skip(Int num) => map({ _ }).skip(num)
    [Override] public take(Int num) => map({ _ }).take(num)
    [Override] public reverse() => map({ _ }).reverse()
    
    [Override]
    public firstWhere(func(Pair<K, V>) -> Bool) -> Pair<K, V>//=>
    //buckets.firstNonNull({ _.firstWhere(pair -> pair != null && pair != 0 && func(pair)) })
    {
        for (bucket in buckets) {
            for (pair in bucket) {
                if (pair != null && pair != 0 && func(pair)) {
                    return pair
                }
            }
        }
        
        return null
    }
    
    [Override]
    public forEach(func(Pair<K, V>, Int, HashMap<K, V>)) => this {
        var Int i = 0
        
        for (bucket in buckets) {
            for (pair in bucket) {
                if (pair != null && pair != 0) {
                    func(pair, i++, this)
                }
            }
        }
    }
    
    getBucket(K key) => buckets[(Int)(key.hashCodeLong & (buckets.count - 1))]
    getPair(K key) => getBucket(key).filter(x -> x != null && x.key.equals(key)).first
    
    public remove(K key) -> V {
        let bucket = getBucket(key)
        
        var Int i = 0
        
        for (pair in bucket) {
            if (pair == null) {
                break
            } else if (pair.key.equals(key)) {
                bucket.remove(i)
                count--
                
                return pair.value
            }
            
            i++
        }
        
        return null
    }
    
    public containsKey(K key) => getPair(key) != null
    
    public clone() => new HashMap<K, V>(this)
    
    public toImmutable() => new ImmutableHashMap<K, V>(this)
}