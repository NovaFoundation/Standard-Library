package "nova/datastruct"

import "nova/datastruct/list/List"

class HashMap<K, V> implements List<Pair<K, V>> {
    Pair<K, V>[][] buckets
    
    Int bucketSize
    
    visible Int size
    
    this[K key] -> V {
        get {
            var pair = getPair(key)//?.value
            
            if (pair != null) {
                return pair.value
            }
            
            return null
        }
        
        set {
            getBucket(key).add(new Pair<K, V>(key, value))
            
            return this
        }
    }
    
    public construct(Int bucketCount = 5, Int bucketSize = 5) {
        buckets = new Array(bucketCount).map({ new Array<Pair<K, V>>(bucketSize) })
        
        this.bucketSize = bucketSize
    }
    
    [Override]
    public toArray() => map({ _ })
    
    [Override]
    public contains(Pair<K, V> value) -> Bool {
        Pair<K, V> pair = getPair(value.key)
        
        if (pair != null) {
            return pair.value == value.value// || (pair.value != null && pair.value.equals(value.value))
        }
        
        return false
    }
    
    [Override]
    public any(func(Pair<K, V>) -> Bool) -> Bool {
        for (bucket in buckets) {
            for (pair in bucket) {
                if (pair != null && pair != 0 && func(pair)) {
                    return true
                }
            }
        }
        
        return false
    }
    
    [Override]
    public all(func(Pair<K, V>) -> Bool) -> Bool {
        for (bucket in buckets) {
            for (pair in bucket) {
                if (pair != null && pair != 0 && !func(pair)) {
                    return false
                }
            }
        }
        
        return true
    }
    
    [Override]
    public map<Out>(func(Pair<K, V>, Int, HashMap<K, V>) -> Out) -> Out[] {
        Out[] array = new Array()
        
        Int i = 0
        
        for (bucket in buckets) {
            for (pair in bucket) {
                if (pair != null && pair != 0) {
                    array.add(func(pair, i++, this))
                }
            }
        }

        return array
    }
    
    [Override]
    public filter(func(Pair<K, V>, Int, HashMap<K, V>) -> Bool) -> Pair<K, V>[] {
        Pair<K, V>[] array = new Array()
        
        Int i = 0
        
        for (bucket in buckets) {
            for (pair in bucket) {
                if (pair != null && pair != 0 && func(pair, i++, this)) {
                    array.add(pair)
                }
            }
        }

        return array
    }
    
    [Override]
    public join(String delimiter) -> String {
        Int i = 0
        
        String output = ""
        
        for (bucket in buckets) {
            for (pair in bucket) {
                if (pair != null && pair != 0) {
                    if (i > 0) {
                        output = output + delimiter
                    }
                    
                    output = output + pair
                }
            }
        }

        return output
    }
    
    [Override] public skip(Int num) => map({ _ }).skip(num)
    [Override] public take(Int num) => map({ _ }).take(num)
    [Override] public reverse() => map({ _ }).reverse()
    
    [Override]
    public firstWhere(func(Pair<K, V>) -> Bool) -> Pair<K, V>//=>
    //buckets.firstNonNull({ _.firstWhere(pair -> pair != null && pair != 0 && func(pair)) }) {
        for (bucket in buckets) {
            for (pair in bucket) {
                if (pair != null && pair != 0 && func(pair)) {
                    return pair
                }
            }
        }
        
        return null
    }
    
    [Override]
    public forEach(func(Pair<K, V>, Int, HashMap<K, V>)) => this {
        //HashMap<K, V> self = this
        
        /*buckets.forEach( {
            _.forEach(pair -> {
                if (pair != null && pair != 0) {
                    func(pair, i++, self)
                }
            })
        })*/
        
        Int i = 0
        
        for (bucket in buckets) {
            for (pair in bucket) {
                if (pair != null && pair != 0) {
                    func(pair, i++, this)
                }
            }
        }
    }
    
    getBucket(K key) => buckets[(Int)(key.hashCodeLong & (buckets.count - 1))]
    getPair(K key) => getBucket(key).filter(x -> x != 0 && x != null && x.key.equals(key)).first
    
    public remove(K key) -> V {
        Pair<K, V>[] bucket = getBucket(key)
        
        Int i = 0
        
        for (pair in bucket) {
            if (pair.key.equals(key)) {
                bucket.remove(i)
                
                return pair.value
            }
            
            i++
        }
        
        return null
    }
    
    public containsKey(K key) => getPair(key) != null
}