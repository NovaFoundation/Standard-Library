package "nova/exception"

import "signal.h"
import "NovaExceptionHandling.h"

class ExceptionData {
    external type buffer
    external buffer* NULL
    
    visible buffer* buf
    
    visible ExceptionData parent
    
    visible Class[] caught
    
    public Exception thrownException
    
    public construct(buffer* buf) {
        this.buf = buf
        this.caught = new Class[]
    }
    
    public addCaught(Class exceptionClass) {
        caught.add(exceptionClass)
    }
    
    public getDataByException(Exception exception) -> ExceptionData {
        ExceptionData data = this
        
        repeat {
            if (data.caught.any({ exception.class.isOfType(_) }) || data.parent == null) {
                return data
            }
            
            data = data.parent
        }
    }
    
    public jumpToBuffer(Exception exception) {
        ExceptionData data = getDataByException(exception)
        
        /*if (data.parent == null) {
            exception = null
        }*/
        
        external c {
            jump(*#{data.buf}, (intptr_t)#{exception});
        }
    }
}