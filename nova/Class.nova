package "nova"

import "nova/meta/Field"

[AutoFinal]
immutable class Class {
    visible Class extension
    visible immutable Class[] interfaces
    
    visible immutable Field[] fields = new Field[0]
    
    visible Bool isInterface
    
    visible String location
    visible String package => location.substring(0, location.lastIndexOf("/", defaultReturnValue: 0))
    visible String name => location.substring(location.lastIndexOf("/") + 1)
    
    visible static immutable Class[] ALL
    
    public construct(visible String location, visible Bool isInterface) {}
    
    public pure isOfType(Class other) =>
        other.isInterface ?
            isOfTypeInterface(other) :
            isOfTypeClass(other)
    
    pure isOfTypeClass(Class other) -> Bool {
        var current = this
        
        while (current) {
            if (current == other) {
                return true
            }
            
            current = current.extension
        }
        
        return false
    }
    
    pure isOfTypeInterface(Class other) -> Bool {
        var current = this
        
        while (current) {
            if (current.interfaces.any({ _ == other })) {
                return true
            }
            
            current = current.extension
        }
        
        return false
    }
    
    public toString() => "Class \"#location\""
}